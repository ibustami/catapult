apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = 'catapult'
                    description = 'Module to simplify data communication and user interaction'
                    url = 'https://github.com/ibustami/catapult'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'mismaiti'
                            name = 'isral'
                            email = 'mismaiti@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/ibustami/catapult.git'
                        developerConnection = 'scm:git:ssh://github.com/ibustami/catapult.git'
                        url = 'https://github.com/ibustami/catapult'
                    }
                }


            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            project.rootProject.localProperties.getProperty('signing.keyId'),
            project.rootProject.localProperties.getProperty('signing.key'),
            project.rootProject.localProperties.getProperty('signing.password')
    )
    sign publishing.publications
}
